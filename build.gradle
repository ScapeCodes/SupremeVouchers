plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
	id("com.gradleup.shadow") version "8.3.3"
}

group = 'net.scape.project'
version = '1.0.5'

java {
    disableAutoTargetJvm()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()

    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://repo.codemc.org/repository/maven-snapshots/" }
    maven { url 'https://repo.codemc.io/repository/creatorfromhell/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://repo1.maven.org/maven2/' }
    maven { url 'https://repo.rosewooddev.io/repository/public/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.nexomc.com/releases/' }
    maven { url 'https://repo.nexomc.com/snapshots' }
    maven { url 'https://repo.oraxen.com/releases' }
    maven { url "https://repo.oraxen.com/releases" }
    maven { url "https://repo.nightexpressdev.com/releases" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven {
        name "essentialsx-releases"
        url "https://repo.essentialsx.net/releases/"
    }
    maven { url "https://repo.alessiodp.com/releases/" }
    maven { url = "https://repo.lushplugins.org/releases/" }
    maven {
        url = "https://repo.artillex-studios.com/releases/"
    }
}

configurations.all {
    resolutionStrategy {
        force 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'
        eachDependency {
            if (requested.group == 'org.spigotmc' && requested.name == 'spigot-api') {
                useTarget 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'
                because 'Force resolution to match Paper 1.21.10 API'
            }
        }
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.11.7'
    compileOnly 'net.milkbowl.vault:VaultUnlockedAPI:2.11'

    compileOnly 'com.github.LoneDev6:itemsadder-api:3.6.3-beta-14'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.2'
    compileOnly 'com.nexomc:nexo:1.6.0'
    compileOnly 'io.th0rgal:oraxen:1.190.0'
    compileOnly 'io.github.valerashimchuck:simpleitemgenerator-api:1.5.0'

    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.15.3'

    compileOnly 'me.chancesd.pvpmanager:pvpmanager:4.0.7'

    implementation "net.kyori:adventure-text-minimessage:4.25.0"
    implementation 'com.github.cryptomorin:XSeries:13.5.1'
    implementation 'com.artillexstudios.axapi:axapi:1.4.802:all'
}

processResources {
    def props = [
            version: project.version,
            name: project.name
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveBaseName.set("SupremeVouchers")
    archiveClassifier.set("")

    // Relocations - critical to avoid conflicts with other plugins!

    relocate 'net.kyori.adventure', 'net.scape.project.shaded.kyori.adventure'
    relocate 'net.kyori.examination', 'net.scape.project.shaded.kyori.examination'
    relocate 'net.kyori.option', 'net.scape.project.shaded.kyori.option'

    relocate 'com.cryptomorin.xseries', 'net.scape.project.shaded.xseries'

    relocate 'com.artillexstudios', 'net.scape.project.shaded.axapi'
    relocate 'revxrsal', 'net.scape.project.shaded.axapi.revxrsal'

    mergeServiceFiles()

    minimize()

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    if (project.hasProperty('dev')) {
        def devBuildVersion = project.findProperty('devBuildVersion') ?: 'SNAPSHOT'
        archiveVersion.set("${project.version}-DEV-${devBuildVersion}")
    } else {
        archiveVersion.set(project.version.toString())
    }

    manifest {
        attributes(
            'Main-Class': 'net.scape.project.supremeVouchers.SupremeVouchers',
            'Implementation-Title': 'SupremeVouchers',
            'Implementation-Version': project.version.toString()
        )
    }
}

tasks.build {
    dependsOn shadowJar
}

tasks.jar {
    enabled = false
}
